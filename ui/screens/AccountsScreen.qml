import QtQuick
import QtQuick.Layouts
import QtQuick.Controls

Item {
    id: root
    property int activeAccountId: 1

    function openEditor(accData = null) {
        if (accData) {
            accountDialog.editingIndex = accData.index
            accountDialog.title = "–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å –∞–∫–∫–∞—É–Ω—Ç"
            nameField.text = accData.name
            numberField.text = accData.number
            serverField.text = accData.server
            portField.text = accData.port.toString()
            protoField.currentIndex = protoField.model.indexOf(accData.proto);
        } else {
            accountDialog.editingIndex = -1
            accountDialog.title = "–ù–æ–≤—ã–π –∞–∫–∫–∞—É–Ω—Ç"
            nameField.clear()
            numberField.clear()
            serverField.text = "sip.test.com"
            portField.text = "5060"
            protoField.currentIndex = 0;

        }
        accountDialog.open()
    }


    // –≠–ª–µ–º–µ–Ω—Ç —Å–ø–∏—Å–∫–∞
    Component {
        id: accountDelegate
        ItemDelegate {
            id: delegateRoot
            width: accountsList.width

            readonly property bool isExpanded: model.accId === accountsList.expandedAccountId
            readonly property bool isActive: model.accId === root.activeAccountId

            contentItem: ColumnLayout {
                spacing: 0
                // –í—Å–µ–≥–¥–∞ –≤–∏–¥–∏–º–∞—è —á–∞—Å—Ç—å
                RowLayout {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 70
                    spacing: 15

                    // "–ª–∞–º–ø–æ—á–∫–∞" —Å—Ç–∞—Ç—É—Å–∞ —Ä–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏–∏
                    Rectangle {
                        id: statusLed
                        width: 10; height: 10; radius: 5
                        Layout.alignment: Qt.AlignVCenter

                        color: {
                            if (model.regStatus === "registered") return "#4CAF50" // –ó–µ–ª–µ–Ω—ã–π
                            if (model.regStatus === "registering") return "#FFC107" // –ñ–µ–ª—Ç—ã–π
                            if (model.regStatus === "error") return "#F44336"      // –ö—Ä–∞—Å–Ω—ã–π
                            return "#BDBDBD"                                      // –°–µ—Ä—ã–π (offline)
                        }

                        // –ê–Ω–∏–º–∞—Ü–∏—è –ø—Ä–∏ –ø–æ–ø—ã—Ç–∫–µ –ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è
                        SequentialAnimation {
                            running: model.regStatus === "registering"
                            loops: Animation.Infinite

                            OpacityAnimator {
                                target: statusLed
                                from: 0.3
                                to: 1
                                duration: 1000
                            }

                            OpacityAnimator {
                                target: statusLed
                                from: 1
                                to: 0.3
                                duration: 1000
                            }
                        }
                    }
                    // –ò–º—è –∞–∫–∫–∞—É–Ω—Ç–∞ –∏ –Ω–æ–º–µ—Ä –∏–ª–∏ –æ—à–∏–±–∫–∞ —Ä–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏–∏
                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 2
                        Label {
                            text: model.name; font.bold: true; font.pixelSize: 16
                            color: delegateRoot.isActive ? "#2196F3" : "black"
                        }
                        Label {
                            text: model.regStatus === "error" ? "–û—à–∏–±–∫–∞: " + model.lastError : model.number
                            font.pixelSize: 11
                            color: model.regStatus === "error" ? "#F44336" : "#757575"
                        }
                    }
                    // –ü–µ—Ä–µ–∫–ª—é—á–∞—Ç–µ–ª—å –¥–ª—è —Ä–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏–∏
                    Switch {
                        id: regSwitchs
                        Layout.alignment: Qt.AlignVCenter

                        onToggled: {
                            // TODO: —Ä–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏—è
                        }
                    }
                    // –ö–Ω–æ–ø–∫–∞ –ø–µ—Ä–µ–∫–ª—é—á–µ–Ω–∏—è –∞–∫—Ç–∏–≤–Ω–æ–≥–æ –∞–∫–∫–∞—É–Ω—Ç–∞
                    Button {
                        text: "–í—ã–±—Ä–∞—Ç—å"
                        visible: !delegateRoot.isActive
                        Layout.alignment: Qt.AlignVCenter
                        onClicked: root.activeAccountId = model.accId
                    }

                    Label {
                        text: isActive ? "–ê–∫—Ç–∏–≤–µ–Ω" : ""
                        color: "#4caf50"; font.pixelSize: 12
                        visible: delegateRoot.isActive
                        Layout.alignment: Qt.AlignVCenter
                    }
                    // –ü—É—Å—Ç–æ–µ –º–µ—Å—Ç–æ –¥–ª—è alignment right
                    Item {
                        Layout.fillWidth: true
                    }
                    // –ö–Ω–æ–ø–∫–∞ –∏–∑–º–µ–Ω–µ–Ω–∏—è –∞–∫–∫–∞—É–Ω—Ç–∞
                    Button {
                        text: "üîß"
                        Layout.alignment: Qt.AlignVCenter | Qt.AlignRight
                        flat: true
                        onClicked: root.openEditor(model)
                    }
                    // –ö–Ω–æ–ø–∫–∞ —É–¥–∞–ª–µ–Ω–∏—è –∞–∫–∫–∞—É–Ω—Ç–∞
                    Button {
                        text: "x"
                        Layout.alignment: Qt.AlignVCenter | Qt.AlignRight
                        flat: true
                        onClicked: accountsModel.remove(index)
                    }
                }

                // –î–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω–∞—è –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è
                ColumnLayout {
                    id: detailBox
                    Layout.fillWidth: true

                    visible: delegateRoot.isExpanded

                    Rectangle { Layout.fillWidth: true; height: 1; color: "#eee" }

                    GridLayout {
                        columns: 2
                        columnSpacing: 20
                        Layout.margins: 10

                        Label { text: "–°–µ—Ä–≤–µ—Ä:"; color: "gray" }
                        Label { text: model.server; font.family: "Monospace" }

                        Label { text: "–ü–æ—Ä—Ç:"; color: "gray" }
                        Label { text: model.port }

                        Label { text: "–ü—Ä–æ—Ç–æ–∫–æ–ª:"; color: "gray" }
                        Label { text: model.proto; font.bold: true }
                    }
                }
            }

            onClicked: {
                if (isExpanded)
                    accountsList.expandedAccountId = -1
                else
                    accountsList.expandedAccountId = model.accId
            }

            background: Rectangle {
                color: delegateRoot.isActive ? "#f0f9ff" : "white"
                border.color: delegateRoot.isActive ? "#bae7ff" : "#eee"
                radius: 8
            }
        }
    }
    Dialog {
        id: accountDialog
        anchors.centerIn: parent
        width: parent.width * 0.8
        modal: true
        standardButtons: Dialog.Save | Dialog.Cancel

        property int editingIndex: -1

        ColumnLayout {
            width: parent.width
            TextField { id: nameField; placeholderText: "–ù–∞–∑–≤–∞–Ω–∏–µ"; Layout.fillWidth: true }
            TextField { id: numberField; placeholderText: "–ù–æ–º–µ—Ä/–õ–æ–≥–∏–Ω"; Layout.fillWidth: true }
            RowLayout {
                TextField { id: serverField; placeholderText: "–°–µ—Ä–≤–µ—Ä"; Layout.fillWidth: true }
                TextField { id: portField; placeholderText: "–ü–æ—Ä—Ç"; Layout.preferredWidth: 80; inputMethodHints: Qt.ImhDigitsOnly }
                ComboBox { id: protoField; model: ["UDP", "TCP", "TLS"]; }
            }
        }

        onAccepted: {
            let data = {
                "name": nameField.text,
                "number": numberField.text,
                "server": serverField.text,
                "port": parseInt(portField.text),
                "proto": protoField.currentText,
                "regStatus": "offline",
                "accId": editingIndex === -1 ? Date.now() : accountsModel.get(editingIndex).accId
            }

            if (editingIndex === -1) {
                accountsModel.append(data)
            } else {
                accountsModel.set(editingIndex, data)
            }
        }
    }    
    ListModel {
        id: accountsModel
        ListElement {
            accId: 1
            name: "–†–∞–±–æ—á–∏–π"
            number: "10112312"
            server: "sip.company.com"
            port: 5060
            proto: "UDP"
            regStatus: "registered"
        }
        ListElement {
            accId: 2
            name: "–î–æ–º–∞—à–Ω–∏–π"
            number: "74951234567"
            server: "sip.provider.net"
            port: 5061
            proto: "TLS"
            regStatus: "registering"
        }
        ListElement {
            accId: 3
            name: "Test"
            number: "test"
            server: "192.168.1.50"
            port: 5060
            proto: "TCP"
            regStatus: "error"
            lastError: "401"
        }
    }
    ColumnLayout {
        anchors.fill: parent
        anchors.margins: 10
        Button {
            text: "+ –î–æ–±–∞–≤–∏—Ç—å –∞–∫–∫–∞—É–Ω—Ç"
            Layout.fillWidth: true
            onClicked: root.openEditor()
        }
        ListView {
            id: accountsList
            Layout.fillHeight: true;
            Layout.fillWidth: true;
            model: accountsModel
            spacing: 10
            clip: true

            property int expandedAccountId: -1

            delegate: accountDelegate
        }
    }
}
